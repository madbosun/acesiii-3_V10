      SUBROUTINE EXPT1(T1PACK,T1FULL,NOCC,NVRT,
     &                 NOCC_EXPND,NVRT_EXPND,NOCCO_EXPND,NVRTO_EXPND,
     &                 NDROPMO,ISPIN)
C
C THIS EXPANDS THE T1 VECTOR FROM A SYMMETRY PACKED ORDER TO A FULL
C NVRT x NOCC VECTOR
C
C Modifications to accomodate dropmo functionality. Ajith Perera, 07/2005.
C
CEND
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER POP,VRT,DIRPRD
      DIMENSION T1PACK(1),T1FULL(NVRT,NOCC)
      DIMENSION NOCC_EXPND(8,2),NVRT_EXPND(8,2),
     &          NOCCO_EXPND(2),NVRTO_EXPND(2)
      COMMON /INFO/ NOCCO(2),NVRTO(2)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NF(4)
      COMMON /FLAGS/ IFLAGS(100)
      COMMON /SYMINF/NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      CALL ZERO(T1FULL,NVRT*NOCC)
      LENGTH=NOCCO(ISPIN)*NVRTO(ISPIN)
      IF (NDROPMO.GT.0) LENGTH=NOCCO_EXPND(ISPIN)*NVRTO_EXPND(ISPIN)
      IOFFO=1
      IOFFV=1
      IOFF0=1
      DO 10 IRREP=1,NIRREP
       NOCIRR=POP(IRREP,ISPIN)
       NVRIRR=VRT(IRREP,ISPIN)
       IF (NDROPMO.GT.0) THEN
          NOCIRR=NOCC_EXPND(IRREP,ISPIN)
          NVRIRR=NVRT_EXPND(IRREP,ISPIN)
       END IF
       DO 20 IOCC=1,NOCIRR
        CALL SCOPY(NVRIRR,T1PACK(IOFF0),1,T1FULL(IOFFV,IOFFO),1)
        IOFFO=IOFFO+1
        IOFF0=IOFF0+NVRIRR
20     CONTINUE
       IOFFV=IOFFV+NVRIRR
10    CONTINUE
      CALL SCOPY(LENGTH,T1FULL,1,T1PACK,1)
      RETURN
      END 
